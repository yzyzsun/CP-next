type HTML = { html : String };
type LaTeX = { latex : String };

type DocSig<Element> = {
  Comp : Element -> Element -> Element;
  Str : String -> Element;
  Endl : Element;
};

html = trait implements DocSig<HTML> => {
  (Comp l r).html = l.html ++ r.html;
  (Str s).html = s;
  (Endl).html = "<br>";
};

latex = trait implements DocSig<LaTeX> => {
  (Comp l r).latex = l.latex ++ r.latex;
  (Str s).latex = s;
  (Endl).latex = "\\\\";
};

type DocSig'<Element> extends DocSig<Element> = {
  Section : Element -> Element;
  SubSection : Element -> Element;
  SubSubSection : Element -> Element;
  Enumerate : Element -> Element;
  Itemize : Element -> Element;
  Item : Element -> Element;
  Href : String -> Element -> Element;
  Bold : Element -> Element;
  Emph : Element -> Element;
};

html' = trait implements DocSig'<HTML> inherits html => {
  (Section e).html = "<h2>" ++ e.html ++ "</h2>";
  (SubSection e).html = "<h3>" ++ e.html ++ "</h3>";
  (SubSubSection e).html = "<h4>" ++ e.html ++ "</h4>";
  (Enumerate e).html = "<ol>" ++ e.html ++ "</ol>";
  (Itemize e).html = "<ul>" ++ e.html ++ "</ul>";
  (Item e).html = "<li>" ++ e.html ++ "</li>";
  (Href s e).html = "<a href=\"" ++ s ++ "\">" ++ e.html ++ "</a>";
  (Bold e).html = "<b>" ++ e.html ++ "</b>";
  (Emph e).html = "<em>" ++ e.html ++ "</em>";
};

latex' = trait implements DocSig'<LaTeX> inherits latex => {
  (Section e).latex = "\\section{" ++ e.latex ++ "}";
  (SubSection e).latex = "\\subsection{" ++ e.latex ++ "}";
  (SubSubSection e).latex = "\\subsubsection{" ++ e.latex ++ "}";
  (Enumerate e).latex = "\\enumerate{" ++ e.latex ++ "}";
  (Itemize e).latex = "\\itemize{" ++ e.latex ++ "}";
  (Item e).latex = "\\item{" ++ e.latex ++ "}";
  (Href s e).latex = "\\href{" ++ s ++ "}{" ++ e.latex ++ "}";
  (Bold e).latex = "\\textbf{" ++ e.latex ++ "}";
  (Emph e).latex = "\\emph{" ++ e.latex ++ "}";
};
